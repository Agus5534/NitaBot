plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.6.0'
    id 'java'
    id("com.github.johnrengelman.shadow") version "7.1.0"
}
def generatedVersionDir = "${buildDir}/generated-version"
def versionInfo = [major: '2', minor: '2', patch: '0']
group 'com.nitabot'
version = versionInfo.values().join('.')
sourceSets {
    main {
        output.dir(generatedVersionDir, builtBy: 'generateVersionProperties')
    }
}
repositories {
    mavenLocal()
    mavenCentral()
    maven {
        url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'

        content {
            includeGroup 'org.bukkit'
            includeGroup 'org.spigotmc'
        }
    }
    maven { url = 'https://oss.sonatype.org/content/repositories/snapshots' }
    maven { url = 'https://oss.sonatype.org/content/repositories/central' }
    maven { url = "https://m2.dv8tion.net/releases"}
    maven { url = "https://m2.chew.pro/releases" }

}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib:1.6.0"
    compileOnly("org.spigotmc:spigot-api:1.16.5-R0.1-SNAPSHOT")
    implementation "net.dv8tion:JDA:4.4.0_351"
    implementation "pw.chew:jda-chewtils:1.23.0"
    implementation 'mysql:mysql-connector-java:8.0.27'
    implementation 'io.github.agus5534:UHCWeekendListener:1.0.0'
}


compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

shadowJar {
    minimize()
}
compileJava.options.encoding = 'UTF-8'

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}
task generateVersionProperties {
    doLast {
        def propertiesFile = file "$generatedVersionDir/version.properties"
        propertiesFile.parentFile.mkdirs()
        def properties = new Properties()
        properties.setProperty("version", rootProject.version.toString())
        propertiesFile.withWriter { properties.store(it, null) }
    }
}
processResources.dependsOn generateVersionProperties